apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

def gitVersionTag() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern

    if (matcher) {
        version = version.substring(0, matcher.start()) + "." + matcher[0][1]
    } else {
        version = version + ".0"
    }

    return version
}

android {
    signingConfigs {
        releaseConfig {
            keyAlias KEYALIAS
            keyPassword KEYPASSWORD
            storeFile file(STOREFILE)
            storePassword STOREPASSWORD
        }
    }
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.hiroshi.cimoc"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.4.12.0"
        resConfigs "en", "zh"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.releaseConfig
        }
        android.applicationVariants.all { variant ->
            if (variant.buildType.name.equals('release')) {
//                variant.mergedFlavor.versionCode = gitVersionCode()
                variant.mergedFlavor.versionName = gitVersionTag()
            }
            variant.outputs.all {
                outputFileName = "Cimoc ${variant.versionName} ${buildType.getName()}.apk"
            }
        }
    }
}

greendao {
    schemaVersion 10
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:support-v13:${SUPPORT_LIB_VERSION}"
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIB_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_LIB_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIB_VERSION}"
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.facebook.fresco:fresco:1.8.0'
    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'org.adw.library:discrete-seekbar:1.0.1'
    implementation 'io.reactivex:rxjava:1.2.4'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.alibaba:fastjson:1.1.70.android'
    implementation files('libs/commons-lang3-3.7.jar')
}
